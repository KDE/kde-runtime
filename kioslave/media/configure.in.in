########### Check for linux/cdrom.h

AC_CHECK_HEADER(linux/cdrom.h,
[
  AC_DEFINE_UNQUOTED([COMPILE_LINUXCDPOLLING], 1, [media linux cd polling compilation])
  LINUXCDPOLLING=yes
  AC_SUBST(LINUXCDPOLLING)
])

########### Check for HAL/DBus

AC_ARG_WITH(hal,AC_HELP_STRING([--with-hal],[Enable HAL support  [default=check]]),[hal_test="$withval"],[hal_test="yes"])

if test "x$hal_test" = "xyes" ; then

  ########### Check for the HAL
  PKG_CHECK_MODULES(HAL, hal >= 0.5, have_hal=yes, have_hal=no)
  PKG_CHECK_MODULES(HALSTORAGE, hal-storage >= 0.5, have_hal_storage=yes, have_hal_storage=no)

  ########### Check for DBus
  PKG_CHECK_MODULES(DBUS, dbus-1 >= 0.30, have_dbus=yes, have_dbus=no)
  
  ########### Check for DBus-Qt bindings
  PKG_CHECK_MODULES(QTDBUS, QtDBUS >= 4.0, have_qtdbus=yes, have_qtdbus=no)
  
fi

########### Check if media HAL backend sould be compiled

AC_MSG_CHECKING(if the HAL backend for media:/ should be compiled)

HALBACKEND=no
if test "x$have_hal" = "xyes" && test "x$have_hal_storage" = "xyes" && test "x$have_dbus" = "xyes" && test "x$have_qtdbus" = "xyes" ; then
  AC_DEFINE_UNQUOTED([COMPILE_HALBACKEND],1, [media HAL backend compilation])
  HALBACKEND=yes
  AC_SUBST(HALBACKEND)
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
fi

AM_CONDITIONAL(include_media_linuxcdpolling, test "$LINUXCDPOLLING" = "yes")
AM_CONDITIONAL(include_media_halbackend, test "$HALBACKEND" = yes)
