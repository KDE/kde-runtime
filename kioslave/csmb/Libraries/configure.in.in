#MIN_CONFIG

AC_ARG_ENABLE(encrypt-pass,[ --disable-encrypt-pass Disable Encrypted Passwords],,enableval="yes")

if test "$enableval" != "no"; then
	AC_DEFINE(PAM_SMB_ENC_PASS)
	AC_MSG_RESULT(Will compile with encrypted password support)
else
	AC_MSG_RESULT(Will compile without encrypted password support)
fi


AC_MSG_CHECKING(for LOG_AUTHPRIV)
AC_EGREP_CPP(yes, 
[#include <syslog.h>
 #ifdef LOG_AUTHPRIV
  yes
 #endif
], has_logauthpriv=yes, has_logauthpriv=no)
AC_MSG_RESULT($has_logauthpriv)
if test "$has_logauthpriv" != "yes"; then
	AC_DEFINE(USE_LOGAUTH)
fi


AC_HEADER_STDC

AC_CHECK_HEADERS(shadow.h security/pam_appl.h unistd.h security/pam_modules.h)

AC_EGREP_HEADER(uid_t, sys/types.h, ac_cv_type_uid_t=yes, ac_cv_type_uid_t=no)
AC_MSG_RESULT($ac_cv_type_uid_t)
if test "$ac_cv_type_uid_t" != "yes"; then
	AC_DEFINE(uid_t, int)
	AC_DEFINE(gid_t, int)
fi

AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h crypt.h sys/select.h \
	sys/ioctl.h sys/stropts.h termio.h termios.h lastlog.h malloc.h \
	sys/sockio.h krb5/krb5.h rpc/rpc.h rpc/key_prot.h sys/m_wait.h \
	sys/param.h ncurses.h paths.h login_cap.h syslog.h sys/wait.h \
	sys/ucred.h sys/mount.h floatingpoint.h fstab.h mntent.h Alib.h)

AC_CHECK_FUNCS(socket powl sqrtl strdup getdtablesize vsnprintf setpgid nice \
	seteuid  snprintf vsyslog initgroups setgroups getgroups grantpt \
	pw_encrypt setpriority getpt unlockpt ptsname setenv unsetenv waitpid)

AC_TYPE_GETGROUPS


dnl This one tries to find XDMDIR for config files
AC_ARG_WITH(xdmdir,
	[  --with-xdmdir	          If the xdm config dir can't be found automaticly],
	[ ac_xdmdir=$withval],
	[ ac_xdmdir="no"])

AC_MSG_CHECKING([for xdm configuration dir])
if test "$ac_xdmdir" = "no"; then
    rm -fr conftestdir
    if mkdir conftestdir; then
	cd conftestdir
    cat > Imakefile <<'EOF'
acfindxdm:
	@echo 'ac_xdmdir="$(XDMDIR)";'
EOF
	if (xmkmf) > /dev/null 2> /dev/null && test -f Makefile; then
	    eval `${MAKE-make} acfindxdm 2>/dev/null 2>/dev/null | grep -v make`
	fi
	cd ..
	rm -fr conftestdir
	dnl Check if Imake was right
	if test -f $ac_xdmdir/xdm-config; then
	    AC_MSG_RESULT($ac_xdmdir)
	else
	    dnl Here we must do something else
	    dnl Maybe look for xdm-config in standard places, and
	    dnl if that fails use a fresh copy in $KDEDIR/config/kdm/
	    AC_FIND_FILE(xdm-config,/etc/X11/xdm /var/X11/xdm /usr/openwin/xdm /usr/X11R6/lib/X11/xdm,ac_xdmdir)
	    if test -f $ac_xdmdir/xdm-config; then
                AC_MSG_RESULT($ac_xdmdir)
            else                                 
		if test "${prefix}" = NONE; then
			ac_xdmdir=$ac_default_prefix/config/kdm
		else
			ac_xdmdir=$prefix/config/kdm
		fi
		AC_MSG_RESULT([xdm config dir not found, installing defaults in $ac_xdmdir])
		xdmconfigsubdir=xdmconfig
		AC_SUBST(xdmconfigsubdir)
	    fi
	fi
    fi
else
    if test -f $ac_xdmdir/xdm-config; then
	AC_MSG_RESULT($ac_xdmdir)
    else

	AC_MSG_RESULT([xdm config dir not found, installing defaults in $ac_xdmdir])
	xdmconfigsubdir=xdmconfig
	AC_SUBST(xdmconfigsubdir)
    fi
fi
AC_DEFINE_UNQUOTED(XDMDIR,"$ac_xdmdir")
AC_SUBST(ac_xdmdir)
